open "std/prelude.jazz"

let interpret = |input| {
    var length = 65535
    var memory = anew(length)
    var ptr = 0
    var c = 0
    var chars = string_to_array(input)
    var i = 0
    while i < len(chars) {
        var ch = chars[i]
        match ch {
            ">" -> if ptr == length - 1 {
                ptr = 0
            } else {
                ptr = ptr + 1
            }
            "<" -> if ptr == 0 {
                ptr = length - 1
            } else {
                ptr = ptr - 1
            }
            "+" -> memory[ptr] = memory[ptr] + 1
            "-" -> memory[ptr] = memory[ptr] - 1
            "." -> print(char_from_int(memory[ptr]))
            "," -> memory[ptr] = getc()
            "[" -> {
                if memory[ptr] == 0 {
                    i = i + 1
                    while c > 0 || chars[i] != "]" {
                        if chars[i] == "[" {
                            c = c + 1
                        } else if chars[i] == "]" {
                            c = c - 1
                        }
                        i = i + 1
                    }
                }
            }
            "]" -> {
                if memory[ptr] != 0 {
                    i = i - 1
                    while c > 0 || chars[i] != "[" {
                        if chars[i] == "]" {
                            c = c + 1
                        } else if chars[i] == "[" {
                            c = c - 1
                        }
                        i = i - 1
                    }
                    i = i - 1
                }
            }
            _ -> {}
        }
        i = i + 1
    }

    println("\n ptr: ", memory[ptr])
}

println("
Brainfuck interpreter

Usage: 
jazz brainf##k.jazz run filename.bf - run brainfuck file
jazz brainf##k.jazz brainfuck_code  - run brainfuck code
jazz brainf##k.jazz                 - run hello world program
")

var arguments = args()
if len(arguments) <= 2 {
    println("interpreting \"Hello,world!\" program")
    interpret("++++++++++[>+++++++>++++++++++>+++>+<<<<-]>++.>+.+++++++..+++.>++.<<+++++++++++++++.>.+++.------.--------.>+.>.")
} else {
    var arg = arguments[3]
    if arg == "run" interpret(fread(arguments[4])) else {interpret(arguments[3])}

}